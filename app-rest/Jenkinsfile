pipeline {

    //-----------------------------------------
    // Declarative Pipeline Syntax Reference :: https://jenkins.io/doc/book/pipeline/syntax/#declarative-pipeline
    // Declarative Pipeline Steps Reference  :: https://jenkins.io/doc/pipeline/steps/
    //-----------------------------------------

    // default agent - docker container
    agent {
        docker {
            image "pduleba/blueocean-agent:latest"
            args '-v /D/docker/agent/.m2:$HOME/.m2'
        }
    }

    // can not be updated - like final
    environment {
        // Access to AWS_ACCESS_KEY_ID & AWS_SECRET_ACCESS_KEY via "CloudBees AWS Credentials Plugin"
        AWS_CREDENTIALS_ID      = "aws-credentials"
        MVN_GLOBAL_SETTINGS_ID  = "global-maven-settings"

        AWS                     = credentials('aws-credentials')
        AWS_DEFAULT_REGION      = credentials('aws-default-region')
        AWS_DEFAULT_OUTPUT      = credentials('aws-default-output')

        ROOT_PATH               = "app-rest"
        TARGET_PATH             = "${env.ROOT_PATH}/target"

        def pom                 = readMavenPom(file: "${env.ROOT_PATH}/pom.xml")
        ARTIFACT_ID             = pom.getArtifactId()
        RELEASE_VERSION         = pom.getVersion().replace("-SNAPSHOT", "")
        RELEASE_BUNDLE_TYPE     = "zip"
    }

    options {
        disableConcurrentBuilds()
    }

    parameters {
        choice choices: ['dev', 'test'],
                name: 'ENVIRONMENT', description: 'Target environment'
        choice choices: ['aws', 'local'],
                name: 'MAVEN_PROFILE', description: 'Maven profile'
        booleanParam defaultValue: true,
                name: 'DRY_RUN', description: 'Release dry run'
        choice choices: ['pduleba-code-deploy-application'],
                name: 'CODE_DEPLOY_APPLICATION', description: 'Code deploy application name'
        choice choices: ['pduleba-code-deploy-deployment-group'],
                name: 'CODE_DEPLOY_GROUP', description: 'Code deploy deployment group name'
        choice choices: ['pduleba-code-deploy-deployment-config'], description: 'Code deploy deployment config name',
                name: 'CODE_DEPLOY_CONFIG'
    }

    tools {
        "maven" "3.6.1"
    }

    stages {

        stage("Configuration review") {
            steps {
                sh "echo AWS_ACCESS_KEY_ID      = ${env.AWS_ACCESS_KEY_ID}"
                sh "echo AWS_SECRET_ACCESS_KEY  = ${env.AWS_SECRET_ACCESS_KEY}"
                sh "echo AWS_DEFAULT_REGION     = ${env.AWS_DEFAULT_REGION}"
                sh "echo AWS_DEFAULT_OUTPUT     = ${env.AWS_DEFAULT_OUTPUT}"

                sh "echo ARTIFACT_ID            = ${env.ARTIFACT_ID}"
                sh "echo RELEASE_VERSION        = ${env.RELEASE_VERSION}"

                sh "echo ENVIRONMENT            = ${params.ENVIRONMENT}"
                sh "echo MAVEN_PROFILE          = ${params.MAVEN_PROFILE}"
                sh "echo DRY_RUN                = ${params.DRY_RUN}"

                // Plugin :: https://wiki.jenkins.io/display/JENKINS/AWS+Parameter+Store+Plugin
                withAWSParameterStore(credentialsId: "${env.AWS_CREDENTIALS_ID}",
                        path: "/pduleba/${params.ENVIRONMENT}",
                        regionName: "${env.AWS_DEFAULT_REGION}",
                        naming: 'relative',
                        recursive: true) {

                    echo "MAVEN_PROFILE         = ${params.MAVEN_PROFILE}"

                    echo "AWS_REGION            = ${env.AWS_DEFAULT_REGION}"
                    echo "AWS_BUCKET            = ${env.APP_BUCKET}"

                    echo "HTTP_HOST             = ${env.HTTP_HOST}"

                    echo "DATABASE_HOST         = ${maskValue(env.DATABASE_NAME)}"
                    echo "DATABASE_PORT         = ${maskValue(env.DATABASE_PORT)}"
                    echo "DATABASE_NAME         = ${maskValue(env.DATABASE_NAME)}"
                    echo "DATABASE_USERNAME     = ${maskValue(env.DATABASE_USERNAME)}"
                    echo "DATABASE_PASSWORD     = ${maskValue(env.DATABASE_PASSWORD)}"
                }

                sh "mvn -B --version"
                sh "echo ${env.MAVEN_HOME}"
                sh "aws --version"
            }
        }

        stage("Maven clean") {
            steps {
                dir("${env.ROOT_PATH}") {
                    sh "mvn -B clean release:clean"
                }
            }
        }

        stage("Maven verify") {
            steps {
                dir("${env.ROOT_PATH}") {
                    sh "mvn -B clean test -P${params.MAVEN_PROFILE}"
                }
            }
        }

        stage("Maven release") {
            steps {
                dir("${env.ROOT_PATH}") {
                    withAWSParameterStore(credentialsId: "${env.AWS_CREDENTIALS_ID}",
                            path: "/pduleba/${params.ENVIRONMENT}",
                            regionName: "${env.AWS_DEFAULT_REGION}",
                            naming: 'relative',
                            recursive: true) {

                        // Plugin :: https://wiki.jenkins.io/display/JENKINS/Config+File+Provider+Plugin
                        configFileProvider([configFile(fileId: "${env.MVN_GLOBAL_SETTINGS_ID}", variable: 'MAVEN_SETTINGS')]) {
                            sh "mvn -B clean release:clean"
                            sh "mvn -B release:prepare ${getReleaseArguments(true)}"
                            sh "mvn -B release:perform ${getReleaseArguments(false)}"
                        }

                        script {
                            EXECUTABLE_ID = "${env.ARTIFACT_ID}-${env.RELEASE_VERSION}"
                            EXECUTABLE_NAME = "${EXECUTABLE_ID}.jar"

                            RELEASE_PACKAGE = "${EXECUTABLE_ID}.${env.RELEASE_BUNDLE_TYPE}"

                            S3_BUCKET = "${env.APP_BUCKET}"
                            S3_KEY = "releases/${params.ENVIRONMENT}/${RELEASE_PACKAGE}"
                            S3_LOCATION = "s3://${S3_BUCKET}/${S3_KEY}"
                        }

                        sh "exec/ux/exec-package.sh ${EXECUTABLE_NAME} ${RELEASE_PACKAGE} ${env.RELEASE_VERSION}"

                        stash name: "release", includes: "target/${RELEASE_PACKAGE}"
                    }
                }
            }
        }

        stage("Is release valid?") {
            steps {
                script {
                    try {
                        timeout(time: 5, unit: "MINUTES") {
                            input message: "Is release ${env.RELEASE_VERSION} valid?", ok: 'Yes'
                        }
                        echo "Release ${env.RELEASE_VERSION} has been approved"
                    } catch (err) {
                        echo "Release ${env.RELEASE_VERSION} has not been approved"
                        throw err
                    }
                }
            }
        }

        stage("S3 copy") {
            when {
                expression {
                    (
                        (currentBuild.result == null || currentBuild.result == 'SUCCESS')
                            &&
                        (params.DRY_RUN == false)
                    )
                }
            }
            steps {
                dir("${env.ROOT_PATH}") {
                    script {
                        unstash name: "release"
                        sh "exec/ux/exec-s3-copy.sh ${RELEASE_PACKAGE} ${S3_LOCATION}"
                    }
                }
            }
        }

        stage("CodeDeploy") {
            when {
                expression {
                    (
                        (currentBuild.result == null || currentBuild.result == 'SUCCESS')
                            &&
                        (params.DRY_RUN == false)
                    )
                }
            }
            steps {
                dir("${env.ROOT_PATH}") {
                    sh "exec/ux/exec-code-deploy.sh " +
                            "${env.CODE_DEPLOY_APPLICATION} ${env.CODE_DEPLOY_GROUP} ${env.CODE_DEPLOY_CONFIG} " +
                            "${S3_BUCKET} ${S3_KEY} ${env.RELEASE_BUNDLE_TYPE}"
                }
                // TODO Add wait for CodeDeploy complete
                // 'aws deploy get-deployment --deployment-id d-QBO3BK49B | jq -r ".deploymentInfo.status"' can be used
            }
        }
    }
}

static def maskValue(String value) {
    return value?.trim() ? "****" : ""
}

String getReleaseArguments(boolean prepare) {
    // INFO :: StringJoiner must be enabled on Jenkins via http://localhost:8181/scriptApproval/
    StringJoiner innerArguments = new StringJoiner(" ")
    innerArguments
            .add("-P${params.MAVEN_PROFILE}")
            .add("-DskipTests")

    if (prepare) {
        innerArguments
                .add("-Dproject.aws.region=${env.AWS_DEFAULT_REGION}")
                .add("-Dproject.aws.bucket=${env.APP_BUCKET}")
                .add("-Dproject.http.host=${env.HTTP_HOST}")
                .add("-Dproject.database.host=${env.DATABASE_HOST}")
                .add("-Dproject.database.port=${env.DATABASE_PORT}")
                .add("-Dproject.database.name=${env.DATABASE_NAME}")
                .add("-Dproject.database.username=${env.DATABASE_USERNAME}")
                .add("-Dproject.database.password=${env.DATABASE_PASSWORD}")
    }

    StringJoiner arguments = new StringJoiner(" ")
            .add("-DdryRun=${params.DRY_RUN}")
            .add("-s")
            .add("${env.MAVEN_SETTINGS}")
            .add("-Darguments=\"")
            .add(innerArguments.toString())
            .add("\"")

    return arguments.toString()
}